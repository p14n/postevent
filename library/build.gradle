import com.vanniktech.maven.publish.SonatypeHost
import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

plugins {
    id 'java'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'com.google.protobuf' version '0.9.2'
    id "com.vanniktech.maven.publish" version "0.31.0"
}

compileJava {
    sourceCompatibility = 21
    targetCompatibility = 21
}

group = 'com.p14n'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.debezium:debezium-api:3.0.1.Final'
    implementation ('io.debezium:debezium-embedded:3.0.1.Final') {
        exclude group: 'org.glassfish.jersey.containers', module: 'jersey-container-servlet'
        exclude group: 'org.glassfish.jersey.inject', module: 'jersey-hk2'
        exclude group: 'org.eclipse.jetty'
    }
    implementation 'io.debezium:debezium-connector-postgres:3.0.1.Final'
    implementation 'io.debezium:debezium-storage-jdbc:3.0.1.Final'
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'com.zaxxer:HikariCP:6.2.1'

    constraints {
        implementation 'com.google.guava:guava:32.0.0-jre'
    }

    // gRPC dependencies
    implementation 'io.grpc:grpc-netty-shaded:1.53.0'
    implementation 'io.grpc:grpc-protobuf:1.53.0'
    implementation 'io.grpc:grpc-stub:1.53.0'
    implementation 'io.grpc:grpc-api:1.53.0'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    
    // For code generation
    implementation 'com.google.protobuf:protobuf-java:3.21.7'
    
    testImplementation platform('io.zonky.test.postgres:embedded-postgres-binaries-bom:16.2.0')
    testImplementation 'io.zonky.test:embedded-postgres:2.0.7'
    testImplementation 'net.jqwik:jqwik:1.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.4.11'
    testImplementation 'org.mockito:mockito-core:3.12.4'

    // OpenTelemetry core dependencies
    implementation 'io.opentelemetry:opentelemetry-api:1.32.0'
    
    // Instrumentation for GRPC
    implementation 'io.opentelemetry.instrumentation:opentelemetry-grpc-1.6:1.32.0-alpha'

    // Logging integration
    //implementation 'io.opentelemetry:opentelemetry-extension-logging:1.32.0'
}

testlogger {
    theme 'standard'
    showExceptions true
    showStackTraces true
    showFullStackTraces false
    showCauses true
    slowThreshold 2000
    showSummary true
    showSimpleNames false
    showPassed true
    showSkipped true
    showFailed true
    showStandardStreams false
    showPassedStandardStreams true
    showSkippedStandardStreams true
    showFailedStandardStreams true
}

test {
    useJUnitPlatform {
        includeEngines 'jqwik', 'junit-jupiter'
    }
    maxHeapSize = "1G"
    minHeapSize = "512M"
    maxParallelForks = 1
    failFast = true
    testLogging.showStandardStreams = true
}
task fastTest( type: Test ) {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
        exclude '**/dst/**'

    }
}

jar {
    manifest {
    }
}

task runHelloWorld(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.p14n.postevent.HelloWorld'
}

// Configure Protobuf plugin
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.7'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.53.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// Make sure the generated code is included in the source sets
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

tasks.withType(Jar) {
   duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

mavenPublishing {
 
  configure(new JavaLibrary(new JavadocJar.Javadoc(), true))

  //publishToMavenCentral(SonatypeHost.DEFAULT)
  // or when publishing to https://s01.oss.sonatype.org
  //publishToMavenCentral(SonatypeHost.S01)
  // or when publishing to https://central.sonatype.com/
  publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

  signAllPublications()

  coordinates("com.p14n", "postevent", version)
  
  pom {
    name = "Postevent"
    description = 'A reliable event publishing and consumption system using PostgreSQL and gRPC'
    inceptionYear = "2025"
    url = "https://github.com/p14n/postevent/"
    licenses {
      license {
        name = 'MIT License'
        url = 'https://opensource.org/licenses/MIT'
      }
    }
    developers {
      developer {
       id = 'p14n'
       name = 'Dean Chapman'
       email = 'dean@p14n.com'
      }
    }
    scm {
      connection = 'scm:git:git://github.com/p14n/postevent.git'
      developerConnection = 'scm:git:ssh://github.com:p14n/postevent.git'
      url = 'https://github.com/p14n/postevent'
    }
  }
}